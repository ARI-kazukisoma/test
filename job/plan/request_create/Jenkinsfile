def uuid = ''
def now = null
def transMasterTag = ''

pipeline {
  agent {node { label 'master' }}
  parameters {
    string(name: 'ENV', description: 'Plan環境\n記入例)　Plan09 \n※現在Plan01～Plan06は指定できません。')
    string(name: 'BRANCH', description: 'ブランチ名 \n記入例)　update99-sample')
    text(name: 'MASTER_TAGS', description: "マスタータグ\n\nタグが一つの場合の記入例\n======================\nte:sample\n======================\n※タグが一つの場合は日付は指定しない。\n\nタグが複数の場合の記入例\n======================\nte:sampleA\nte:sampleB&2018/11/11 11:00\nte:sampleC&2018/11/12 12:00\n======================\n※タグが複数の場合、日付を指定しなければ現在時間として処理されます。\n※秒は指定しないでください。")
    text(name: 'REASON', description: '利用用途')

  }
  stages {
    stage("入力チェック") {
      steps {
        script {
          def parameterValidate = load("lib/validateParameter.groovy")
          def slack = load("lib/slack.groovy")
          def template = load("lib/template.groovy")
          def errorMessages = []

          // 環境の入力チェック
          (success, errorCode) = parameterValidate.checkNotExistsEnv(params.ENV)
          if (success == false) {
            errorMessages.push(template.getValidateError(errorCode, 'ENV'))
          }

          // ブランチの入力チェック
          (success, errorCode, minorVersion) = parameterValidate.checkBranch(params.BRANCH)
          if (success == false) {
            errorMessages.push(template.getValidateError(errorCode, 'BRANCH'))
          }

          // マスタータグの入力チェック
          (success, _transMasterTag, errorCode) = parameterValidate.checkMasterTags(params.MASTER_TAGS)
          if (success == false) {
            errorMessages.push(template.getValidateError(errorCode, 'MASTER_TAGS'))
          }
          transMasterTag = _transMasterTag

          // 利用用途の入力チェック
          (success, errorCode) = parameterValidate.checkReason(params.REASON)
          if (success == false) {
            errorMessages.push(template.getValidateError(errorCode, 'REASON'))
          }

          if (errorMessages.size() > 0) {
            message = template.getValidateAllError(errorMessages)
            slack.errorMessage('planner_channel', message)
            error(message)
          }
          
        }
      }
    }
    stage("入力パラメータをJSONに保存") {
      steps {
        script {
          def datetime = load("lib/datetime.groovy")
          now = datetime.now()

          def requestUser = ''
          wrap([$class: 'BuildUser']) {
            requestUser = BUILD_USER
          }

          // 実行に必要なパラメータをjsonファイルに保存
          def param = [
            'request_user': requestUser,
            'target_env': params.ENV,
            'branch': params.BRANCH,
            'master_tags': transMasterTag,
            'reason': params.REASON,
            'request_datetime': now,
            'original_master_tags': params.MASTER_TAGS
          ]
          uuid = UUID.randomUUID().toString()
          def filePath = "${env.TMP_PARAMETER_PATH}/${uuid}.json"
          def json = load("lib/json.groovy")
          json.write(param, filePath)
        }
      }
    }
    stage("Slackへ申請送信") {
      steps {
        script {
          def slack = load("lib/slack.groovy")
          def template = load("lib/template.groovy")
          def CONSTS = load("constant/main.groovy").getAll()
          def message = ""

          // 実行ユーザ(BUILD_USER)を取得するためwrapする
          wrap([$class: 'BuildUser']) {
            def binding = [
              'uuid': uuid,
              'request_user': BUILD_USER,
              'target_env': params.ENV,
              'request_datetime': now,
              'branch': params.BRANCH,
              'master_tags': params.MASTER_TAGS,
              'reason': params.REASON,
              'approval_link': "${CONSTS.API_URL.APPROVAL_CREATE_ENV_JOB}&amp;UUID=${uuid}",
              'refusal_link': "${CONSTS.API_URL.REFUSAL_CREATE_ENV_JOB}&amp;UUID=${uuid}"
            ]
            message = template.toString("request_create_env.template", binding)
          }

          slack.notifyMessage("admin_channel", message)
        }
      }
    }
  }
}